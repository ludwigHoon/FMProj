/*
Objectives 1-3:
1. Read input DNA sequence file in FASTA format. 
	FASTA format: 
	>typeName
	data....
	>typeName
	data...
2. Check & process the input file so that sequence with deletion or insertion 
	(i.e. those with length greater or less than the other sequences) are excluded.
3. Notify user of the excluded sequences. 
*/
class Sequences

types
public string = seq of char;
public gene = string;
public typeName = string;

instance variables
public genes : map typeName to gene :={|->}; -- Obj. 1 ==> As many pairs of typeName to data as in file
public excludedGenes : set of typeName :={};

operations
public Sequences: map typeName to gene ==> Sequences
Sequences(input)==
	genes:=input;

--Obj. 2 ==> sequences with insertion / deletion are added to excluded gene & gene to be used for analysis is restricted by excluded genes 
public processInput: () ==> ()
processInput() ==(
	dcl lenMap : map nat to set of typeName:={|->};
	for all a in set dom genes  -- looping through all data, calculate the length of each gene type
		do if (not (calcLen(genes(a)) in set dom lenMap))
		then lenMap(calcLen(genes(a))):= {a}
		else lenMap(calcLen(genes(a))):=lenMap(calcLen(genes(a))) union {a};
	for all a in set dom lenMap  -- looping through all data, adding it to excluded gene if it does not have the same length as most other data
		do if card lenMap(a) <> max({ card lenMap(b) | b in set dom lenMap})
		then excludedGenes:=excludedGenes union lenMap(a);
	genes:=excludedGenes<-:genes;  -- data to be used for analysis is restricted by excluded gene
	);

--Obj. 3 ==> users are notified.
public notify: () ==> string
notify() ==(
	dcl gString : string := "";
	for all a in set excludedGenes
	do gString:=gString ^ a ^ ", ";
	return "Excluded genes are: "^gString);

public getSequence: () ==> map typeName to gene
getSequence() ==
	return genes;

functions
private calcLen: string -> nat
calcLen(gene)==
	len gene;

private max: set of nat -> nat
max(nums)==
	let c in set nums be st 
	forall c' in set nums \ {c} &
	c >= c'
	in c;

end Sequences